%Nat definition%
nat : type.
z : nat.
s nat : nat.

#builtin NAT nat.
#builtin NAT_ZERO z.
#builtin NAT_SUCC s.
%Nat defintion END%

%User definition%
actor : type.

manufacturer_is actor : pred.

entity actor : pred.


ic : type.
stm32f4 : ic.

ic_produced nat : pred.

inc_ic nat nat: bwd.
inc_ic N (s N).


chip ic nat: pred.


%% Chip life cycle states
chip_is_unconfigured ic nat : pred.
chip_is_configured ic nat : pred.
chip_life_is_over ic nat : pred.

own actor ic nat : pred.
%User definition END%

%Stage section%
stage LCM = {

lifeCycle/create_IC
  : $entity X * $manufacturer_is X * ic_produced N * inc_ic N P -o chip stm32f4 P * chip_is_unconfigured stm32f4 P * own X stm32f4 P * ic_produced P.
  
lifeCycle/give_ownership
  : $chip Y N * own X Y N * $entity X * $entity X' -o own X' Y N.
  
lifeCycle/chip_configure
  : $entity X * $chip Y N * $own X Y N * chip_is_unconfigured Y N -o chip_is_configured Y N.
  
lifeCycle/chip_reconfigure
  : $entity X * $chip Y N * $own X Y N * chip_is_configured Y N -o chip_is_configured Y N.
  
lifeCycle/chip_end_of_life
  : chip Y N * chip_is_configured Y N -o !chip_life_is_over Y N.

}
#interactive LCM.
%Stage section END%

%context definition%
stmicro : actor.
clientA : actor.
clientB : actor.

context init =
{
  entity stmicro, 
  manufacturer_is stmicro, 
  entity clientA,
  entity clientB,
 
  ic_produced 0
}
%context definition END%

#trace _ LCM init.

